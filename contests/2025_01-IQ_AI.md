# IQ AI

## [H-01] Quorum is reached with less votes than expected

### Summary

The contract `TokenGovernor.sol` implements a voting mecanism inherited from Openzeppelin `GovernorVotesQuorumFraction` and `Governor`.

In order for a vote to be executed, quorum must be reached.

The protocol's intention is to set this quorum to be 1/4 (25%) of the token supply. This can be specified as an argument to the `GovernorVotesQuorumFraction()` constructor.

However, passing the value `4` means that quorum is reached at 4% of the token supply.

<https://github.com/code-423n4/2025-01-iq-ai/blob/main/src/TokenGovernor.sol#L55>

```solidity
constructor(
    string memory _name,
    IVotes _token,
    Agent _agent
)
    Governor(_name)
    GovernorVotes(_token)
@>  GovernorVotesQuorumFraction(4) // quorum is 25% (1/4th) of supply
{
    agent = _agent;
}
```

The reference can be found [in the OZ guide](https://docs.openzeppelin.com/contracts/5.x/governance#governor) at the `For 2)` section.

### Impact

Quorum can be reached with way less votes than intended, allowing a minority of voters to execute important decision regarding the Agent.

### Proof of Concept

The below PoC is inspired by the `TokenGovernorTest.sol` test provided by the protocol and shows quorum is reached with 5% of the supply (less than 25%).

It can be executed with: `forge test --match-test test_quorumLowerThanExpected`

```solidity
// SPDX-License-Identifier: ISC
pragma solidity >=0.8.25 <0.9.0;

import {Test} from "forge-std/src/Test.sol";
import {console} from "forge-std/src/console.sol";
import {AgentFactory} from "../src/AgentFactory.sol";
import {TokenGovernor} from "../src/TokenGovernor.sol";
import {Agent} from "../src/Agent.sol";
import {AIToken} from "../src/AIToken.sol";
import {BootstrapPool} from "../src/BootstrapPool.sol";
import {IERC20} from "@openzeppelin/contracts/interfaces/IERC20.sol";
import {LiquidityManager} from "../src/LiquidityManager.sol";

contract WrongQuorumTest is Test {
    IERC20 currencyToken = IERC20(0xFc00000000000000000000000000000000000001);
    address whale = 0x00160baF84b3D2014837cc12e838ea399f8b8478;
    address badActor = address(0xBADBEEF);
    Agent agent;
    AIToken token;
    AgentFactory factory;
    BootstrapPool bootstrapPool;
    LiquidityManager manager;
    TokenGovernor governor;

    function setUpFraxtal(uint256 _block) public {
        vm.createSelectFork(vm.envString("FRAXTAL_MAINNET_URL"), _block);
    }

    function setUp() public {
        setUpFraxtal(12_918_968);
        uint256 creationFee = 15e18;
        uint256 tradingFee = 100; //1%
        uint256 initialSwap = 100e18;
        factory = new AgentFactory(currencyToken, 0);
        factory.setAgentBytecode(type(Agent).creationCode);
        factory.setGovenerBytecode(type(TokenGovernor).creationCode);
        factory.setLiquidityManagerBytecode(type(LiquidityManager).creationCode);
        factory.setTargetCCYLiquidity(1000e18);
        factory.setInitialPrice(0.1e18);
        factory.setTradingFee(tradingFee);
        factory.setCreationFee(creationFee);
        factory.setDefaultProxyImplementation(address(new DefaultProxy()));
        vm.startPrank(whale);
        currencyToken.approve(address(factory), creationFee + initialSwap);
        agent = factory.createAgent("AIAgent", "AIA", "https://example.com", initialSwap);
        token = AIToken(address(agent.token()));

        // Buy from the bootstrap pool
        manager = LiquidityManager(factory.agentManager(address(agent)));
        bootstrapPool = manager.bootstrapPool();
        currencyToken.approve(address(bootstrapPool), type(uint256).max);
        uint256 fivePercentOfSupply = token.totalSupply() * 500 / 10_000;
        uint256 amountIn = bootstrapPool.getAmountIn(fivePercentOfSupply, address(token));
        bootstrapPool.buy(amountIn);
        vm.stopPrank();

        // asserts whale has less than 10% of the total supply
        assertLt(
            token.balanceOf(whale),
            token.totalSupply() * 1000 / 10_000
        );

        vm.warp(block.timestamp + 1);

        governor = TokenGovernor(payable(agent.owner()));
        console.log("votingDelay:", governor.votingDelay());
    }

    function test_test_quorumLowerThanExpected() public {
        vm.expectRevert(); // Proxy not yet set
        console.log(AirdropAgent(payable(agent)).hello());

        // Set the airdropAgentProxy implementation as allowed
        AirdropAgent airdropAgentProxy = new AirdropAgent(
            "AirdropAgent",
            "ADA",
            "https://example.com",
            address(factory)
        );
        factory.setAllowedProxyImplementation(address(airdropAgentProxy), true);

        // Set agent as alive
        factory.setAgentStage(address(agent), 1);

        address[] memory targets = new address[](1);
        targets[0] = address(agent);
        uint256[] memory values = new uint256[](1);
        bytes[] memory calldatas = new bytes[](1);
        calldatas[0] = abi.encodeWithSignature("setProxyImplementation(address)", address(airdropAgentProxy));
        string memory description = "Set the airdropAgentProxy implementation";

        vm.startPrank(whale);
        token.delegate(whale);
        vm.warp(block.timestamp + 1);
        uint256 nonce = governor.propose(targets, values, calldatas, description);

        vm.warp(block.timestamp + governor.votingDelay() + 1);
        governor.castVote(nonce, 1);

        vm.warp(block.timestamp + governor.votingPeriod());
        governor.execute(targets, values, calldatas, keccak256(abi.encodePacked(description)));
        vm.stopPrank();

        console.log(AirdropAgent(payable(agent)).hello());
    }
}

contract AirdropAgent is Agent {
    constructor(
        string memory name,
        string memory symbol,
        string memory url,
        address _factory
    ) Agent(name, symbol, url, _factory) {}

    function airdropTokens(address[] memory _recipients, uint256 _amount) public onlyOwner {
        for (uint256 i = 0; i < _recipients.length; ++i) {
            IERC20(token).transfer(_recipients[i], _amount);
        }
    }

    function hello() public pure returns (string memory) {
        return "Hello";
    }
}

contract DefaultProxy {}
```

### Recommended mitigation steps

Modify the `TokenGovernor` constructor to set quorum at 25% like such:

```diff
constructor(
    string memory _name,
    IVotes _token,
    Agent _agent
)
    Governor(_name)
    GovernorVotes(_token)
-   GovernorVotesQuorumFraction(4) // quorum is 25% (1/4th) of supply
+   GovernorVotesQuorumFraction(25) // quorum is 25% (1/4th) of supply
{
    agent = _agent;
}
```

## [M-01] Pair can be created with less fees than intended by the protocol

### Summary

In `LiquidityManager.sol`, when enough liquidity has been accrued from users buying `AIToken`, the function `moveLiquidity()` can be called to move the liquidity left to a Fraxswap pair.

This function internally calls `addLiquidityToFraxswap()` which deploys the corresponding pair in case it has not been created yet.

In Fraxswap, users can specify the `fee` collected from swaps. In this protocol, the fee is set to 100 (1%) by default.

<https://github.com/code-423n4/2025-01-iq-ai/blob/main/src/AgentFactory.sol#L37>

```solidity
// AgentFactory.sol
contract AgentFactory is Ownable2Step {
    using SafeERC20 for IERC20;
    using SafeERC20 for AIToken;
    /// #### Globals

    /// @notice Token to be paired w/ Agent Token
    IERC20 public currencyToken;
    /// @notice Fee, denominated in `currencyToken`, associated with creating an Agent
    uint256 public creationFee;
    /// @notice Fee to be set w/n `BootstrapPool` contract
@>  uint256 public tradingFee = 100; // 1%

// --- snip ---
    LiquidityManager manager = deployLiquidityManager(
        currencyToken,
        token,
        address(agent),
        initialPrice,
        targetCCYLiquidity,
        initialLiquidity,
@>      tradingFee
    );
```

<https://github.com/code-423n4/2025-01-iq-ai/blob/main/src/LiquidityManager.sol#L140>

```solidity
// LiquidityManager.sol
function addLiquidityToFraxswap(
    uint256 liquidityAmount,
    uint256 currencyAmount
) internal returns (IFraxswapPair fraxswapPair) {
    fraxswapPair = IFraxswapPair(fraxswapFactory.getPair(address(currencyToken), address(agentToken)));
    if (fraxswapPair == IFraxswapPair(address(0))) {
        // Create Fraxswap pair and add liquidity
@>      fraxswapPair = IFraxswapPair(fraxswapFactory.createPair(address(currencyToken), address(agentToken), fee));
        agentToken.safeTransfer(address(fraxswapPair), liquidityAmount);
        currencyToken.safeTransfer(address(fraxswapPair), currencyAmount);
        fraxswapPair.mint(address(this));
```

However, any user is able to create the pair by interacting with the Fraxswap Factory directly and provide a fee as low as 0.01%.

### Impact

Fees accrued by swaps will be less than expected, slowing down the benefits of liquidity providers, disincentiving them from depositing liquidity and contributing to the durability of the `AIToken`

### Proof of Concept

Assume the price of 1 `AIToken` is 0.1 `CurrencyToken` (ratio is 10:1)

1. User buys 10 `AIToken` with 1 `CurrencyToken`
2. User calls `fraxswapFactory::createPair(AIToken, CurrencyToken, 1)` to create the pair with 0.01% fees

### Mitigation

Deploy the pair with the intended fees either:

* at `AgentFactory::createAgent()` time
* at `BootstrapPool::buy()` time

Basically at a moment when the very first AIToken can be obtained by external users.
